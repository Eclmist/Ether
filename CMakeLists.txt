#
#    This file is part of Ether, an open-source DirectX12 renderer.
#   
#    Copyright (c) 2020-2021 Samuel Van Allen - All rights reserved.
#   
#    Ether is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
#   
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#   
#    You should have received a copy of the GNU General Public License
#    along with this program.  If not, see <http://www.gnu.org/licenses/>.
#   

cmake_minimum_required (VERSION "3.16")

# =========================================================================== #
#                     PROJECT DEFINITIONS & PROPERTIES                        #
# =========================================================================== #

# Setup the name of the solution. This will change some of the directories naming convention
# generated by CMake. Ignore CXX in the second param since CMake supports C++ by default
project (Ether)

set(ENGINE_PROJECT ${PROJECT_NAME}.Engine)
set(TOOLMODE_PROJECT ${PROJECT_NAME}.Toolmode)

set_property(DIRECTORY PROPERTY VS_STARTUP_PROJECT ${ENGINE_PROJECT})

# Ensure running on Windows
if (NOT WIN32) 
    message(FATAL_ERROR "${PROJECT_NAME} is only supported on Windows 10! CMake will exit.")
endif ()

if (NOT MSVC)
    message(FATAL_ERROR "${PROJECT_NAME} can only be built on Visual Studio! CMake will exit.")
endif ()

# Sets the C++ versions
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

# Sets project output directories
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/bin)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/bin)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/bin)

# =========================================================================== #
#                        ADD SOURCE FILES TO PROJECT                          #
# =========================================================================== #

include_directories("${CMAKE_CURRENT_SOURCE_DIR}/src")
include_directories("${CMAKE_CURRENT_SOURCE_DIR}/resource")
include_directories("${CMAKE_CURRENT_SOURCE_DIR}/include")

file(GLOB_RECURSE project_headers src/*.h)
file(GLOB_RECURSE project_cpps src/*.cpp)
file(GLOB_RECURSE project_shaders src/*.hlsl)
file(GLOB_RECURSE extern_files include/*)
file(GLOB_RECURSE resources resource/*)
set_source_files_properties(${project_shaders} PROPERTIES VS_TOOL_OVERRIDE "None")
set_source_files_properties(src/ether.cpp PROPERTIES COMPILE_FLAGS "/Ycether.h")

set(all_files ${project_headers} ${project_cpps} ${project_shaders} ${extern_files} ${resources})
set(engine_files_only ${all_files})
list(FILTER engine_files_only EXCLUDE REGEX "src/toolmode/*")

source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${all_files})
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${engine_files_only})
# =========================================================================== #
#                   SETUP THIRD PARTY/EXTERN DEPENDENCIES                     #
# =========================================================================== #

file(GLOB_RECURSE imgui include/imgui/*)
set(imgui_files ${imgui})

# =========================================================================== #
#                          SET COMPILATION TARGETS                            #
# =========================================================================== #

# ================================= ENGINE ================================== #

add_library(${ENGINE_PROJECT} SHARED ${engine_files_only})
target_compile_definitions(${ENGINE_PROJECT} PRIVATE ETH_ENGINE)

target_compile_options(${ENGINE_PROJECT} PRIVATE "/Yuether.h")
target_compile_options(${ENGINE_PROJECT} PRIVATE "/FIether.h")

# ================================ TOOLMODE ================================= #

add_executable(${TOOLMODE_PROJECT} WIN32 ${all_files})
target_compile_definitions(${TOOLMODE_PROJECT} PRIVATE ETH_TOOLMODE)

target_compile_options(${TOOLMODE_PROJECT} PRIVATE "/Yuether.h")
target_compile_options(${TOOLMODE_PROJECT} PRIVATE "/FIether.h")

set_property(TARGET ${TOOLMODE_PROJECT} PROPERTY VS_DEBUGGER_WORKING_DIRECTORY "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${CMAKE_CFG_INTDIR}")


# =========================================================================== #
#                          LINK EXTERNAL LIBRARIES                            #
# =========================================================================== #

# Link DX12
target_link_libraries(${ENGINE_PROJECT} d3d12.lib dxgi.lib dxguid.lib dxcompiler.lib)
target_link_libraries(${TOOLMODE_PROJECT} d3d12.lib dxgi.lib dxguid.lib dxcompiler.lib ws2_32.lib)

# =========================================================================== #
#                             INCLUDE SUBPROJECTS                             #
# =========================================================================== #

add_subdirectory(sampleapp)

